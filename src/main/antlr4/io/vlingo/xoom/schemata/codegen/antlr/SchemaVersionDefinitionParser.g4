// Copyright Â© 2012-2021 VLINGO LABS. All rights reserved.
//
// This Source Code Form is subject to the terms of the
// Mozilla Public License, v. 2.0. If a copy of the MPL
// was not distributed with this file, You can obtain
// one at https://mozilla.org/MPL/2.0/.

parser grammar SchemaVersionDefinitionParser;

options { tokenVocab=SchemaVersionDefinitionLexer; }

typeDeclaration
  : (type typeName typeBody) EOF
  ;

type
  : COMMAND
  | DATA
  | DOCUMENT
  | ENVELOPE
  | EVENT
  | METADATA
  ;

typeName
  : TYPE_IDENTIFIER
  ;

typeBody
  : LBRACE attributes RBRACE
  ;

attributes
  : attribute*
  ;

attribute
  : basicTypeAttribute
  | complexTypeAttribute
  | specialTypeAttribute
  ;

basicTypeAttribute
  : BOOLEAN IDENTIFIER (ASSIGN BOOLEAN_LITERAL)?
  | BOOLEAN ARRAY IDENTIFIER (ASSIGN LBRACE BOOLEAN_LITERAL (COMMA BOOLEAN_LITERAL)* RBRACE)?
  | BYTE IDENTIFIER (ASSIGN BYTE_LITERAL)?
  | BYTE ARRAY IDENTIFIER (ASSIGN LBRACE BYTE_LITERAL (COMMA BYTE_LITERAL)* RBRACE)?
  | CHAR IDENTIFIER (ASSIGN CHAR_LITERAL)?
  | CHAR ARRAY IDENTIFIER (ASSIGN LBRACE CHAR_LITERAL (COMMA CHAR_LITERAL)* RBRACE)?
  | DOUBLE IDENTIFIER (ASSIGN FLOAT_LITERAL)?
  | DOUBLE ARRAY IDENTIFIER (ASSIGN LBRACE FLOAT_LITERAL (COMMA FLOAT_LITERAL)* RBRACE)?
  | FLOAT IDENTIFIER (ASSIGN FLOAT_LITERAL)?
  | FLOAT ARRAY IDENTIFIER (ASSIGN LBRACE FLOAT_LITERAL (COMMA FLOAT_LITERAL)* RBRACE)?
  | INT IDENTIFIER (ASSIGN DECIMAL_LITERAL)?
  | INT ARRAY IDENTIFIER (ASSIGN LBRACE DECIMAL_LITERAL (COMMA DECIMAL_LITERAL)* RBRACE)?
  | LONG IDENTIFIER (ASSIGN DECIMAL_LITERAL)?
  | LONG ARRAY IDENTIFIER (ASSIGN LBRACE DECIMAL_LITERAL (COMMA DECIMAL_LITERAL)* RBRACE)?
  | SHORT IDENTIFIER (ASSIGN DECIMAL_LITERAL)?
  | SHORT ARRAY IDENTIFIER (ASSIGN LBRACE DECIMAL_LITERAL (COMMA DECIMAL_LITERAL)* RBRACE)?
  | STRING IDENTIFIER (ASSIGN (STRING_LITERAL | NULL_LITERAL))?
  | STRING ARRAY IDENTIFIER (ASSIGN LBRACE STRING_LITERAL (COMMA STRING_LITERAL)* RBRACE)?
  ;

complexTypeAttribute
  : typeName IDENTIFIER (ASSIGN NULL_LITERAL)?
  | typeName ARRAY IDENTIFIER
  ;

specialTypeAttribute
  : TIMESTAMP IDENTIFIER
  | TYPE IDENTIFIER
  | VERSION IDENTIFIER
  ;
